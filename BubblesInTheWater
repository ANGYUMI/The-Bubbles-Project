//TAB_NO.1
/**
 * Forces (Gravity and Fluid Resistence) with Vectors 
 * by Daniel Shiffman.  
 * 
 * Demonstration of multiple force acting on bodies (Mover class)
 * Bodies experience gravity continuously
 * Bodies experience fluid resistance when in "water"
 */

// Five moving bodies
Mover[] movers = new Mover[14];

// Liquid
Liquid liquid;

// light direction
float angle = 0.0;    // Current angle
float speed = 0.02;   // Speed of motion
float radius = 1000.0;  // Range of motion

void setup() {
  size(1285, 725, P3D);  
  smooth();
  randomSeed(1);
  reset();
  // Create liquid object
  liquid = new Liquid(0, 0, width, height, 0.1);
}

void draw() {
background(0, 255, 0);

// ------ light direction ------ 
  angle += speed;  // Update the angle
  float sinval = sin(angle);
  float cosval = cos(angle);
  // Set the position of the small circle based on new
  // values from sine and cosine
  float lx = (cosval * radius);
  float ly = (sinval * radius);
  
// ------ light ------ 
  ambientLight(120, 120, 120);
  directionalLight(255, 0, 255, lx, ly, width/2);//
  specular(255, 0, 255); // light color

  pointLight(0, 255, 255, lx, ly, width/2);//highlight color // syn: (r, g, b, x, y, z);
  lightSpecular(255, 0, 255);
  float s = map(mouseX, 0, width, 1, 10);
  
  shininess(s);
  
  
// ------ Draw water ------
  liquid.display();

  for (int i = 0; i < movers.length; i++) {
    
    // Is the Mover in the liquid?
    if (liquid.contains(movers[i])) {
      // Calculate drag force
      PVector dragForce = liquid.drag(movers[i]);
      // Apply drag force to Mover
      movers[i].applyForce(dragForce);
    }

    // Gravity is scaled by mass here!
    PVector gravity = new PVector(0, 0.1*movers[i].mass);
    // Apply gravity
    movers[i].applyForce(gravity);
   
    // Update and display
    movers[i].update();
    movers[i].display();
    movers[i].checkEdges();
  }
  
//text("click mouse to reset",10,30);
  
//  if (frameCount % 20 == 0) saveFrame("ch2_05_####.png");
}

void mousePressed() {
  reset();
}

// Restart all the Mover objects randomly
void reset() {
  for (int i = 0; i < movers.length; i++) {
    movers[i] = new Mover(random(0.5*2.25,3*2.25), 20*2.25+i*40*2.25, 0);
  }
}


//TAB_NO.2
/**
 * Forces (Gravity and Fluid Resistence) with Vectors 
 * by Daniel Shiffman.  
 * 
 * Demonstration of multiple force acting on bodies (Mover class)
 * Bodies experience gravity continuously
 * Bodies experience fluid resistance when in "water"
 */
 
 // Liquid class 
 class Liquid {

  
  // Liquid is a rectangle
  float x,y,w,h;
  // Coefficient of drag
  float c;

  Liquid(float x_, float y_, float w_, float h_, float c_) {
    x = x_;
    y = y_;
    w = w_;
    h = h_;
    c = c_;
  }
  
  // Is the Mover in the Liquid?
  boolean contains(Mover m) {
    PVector l = m.location;
    if (l.x > x && l.x < x + w && l.y > y && l.y < y + h) {
      return true;
    }  
    else {
      return false;
    }
  }
  
  // Calculate drag force
  PVector drag(Mover m) {
    // Magnitude is coefficient * speed squared
    float speed = m.velocity.mag();
    float dragMagnitude = c * speed * speed;

    // Direction is inverse of velocity
    PVector dragForce = m.velocity.get();
    dragForce.mult(-1);
    
    // Scale according to magnitude
    // dragForce.setMag(dragMagnitude);
    dragForce.normalize();
    dragForce.mult(dragMagnitude);
    return dragForce;
  }
  
  void display() {
    noStroke();
//    fill(155, 75, 255);
//    rect(x,y,w,h);
  }

}

//TAB_NO.3
/**
 * Forces (Gravity and Fluid Resistence) with Vectors 
 * by Daniel Shiffman.  
 * 
 * Demonstration of multiple force acting on bodies (Mover class)
 * Bodies experience gravity continuously
 * Bodies experience fluid resistance when in "water"
 */


class Mover {

  // location, velocity, and acceleration 
  PVector location;
  PVector velocity;
  PVector acceleration;
  
  // Mass is tied to size
  float mass;

  Mover(float m, float x, float y) {
    mass = m;
    location = new PVector(x, y);
    velocity = new PVector(0, 0);
    acceleration = new PVector(0, 0);
  }

  // Newton's 2nd law: F = M * A
  // or A = F / M
  void applyForce(PVector force) {
    // Divide by mass 
    PVector f = PVector.div(force, mass);
    // Accumulate all forces in acceleration
    acceleration.add(f);
  }

  void update() {
    
    // Velocity changes according to acceleration
    velocity.add(acceleration);
    // Location changes by velocity
    location.add(velocity);
    // We must clear acceleration each frame
    acceleration.mult(0);
  }
  
// ------ Draw Mover ------
  void display() {
//    stroke(0);
//    strokeWeight(2*2.25);
//    fill(127,200);
//    ellipse(location.x, location.y, mass*16, mass*16);
    
        
//sphere
    pushMatrix();
    translate(location.x, location.y, 0); 
    noStroke();
    fill(225, 125, 255);
    strokeWeight(2);
    sphere(mass*8); //30//100
    popMatrix();
    
  }
  
  // Bounce off bottom of window
  void checkEdges() {
    if (location.y > height*2) {
      velocity.y *= -0.9; //-0.9 // A little dampening when hitting the bottom
      location.y = height*2;//-60
    }
  }
}
